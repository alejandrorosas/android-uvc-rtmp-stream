buildscript {
    ext.kotlin_version = '1.7.10'
    ext.dagger_version = '2.43.1'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_version"
    }
}

allprojects {
    apply from: "$rootDir/ktlint.gradle"

    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://jitpack.io'
            content { includeGroup("com.github.pedroSG94.rtmp-rtsp-stream-client-java") }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 31
                buildToolsVersion "30.0.3"

                defaultConfig {
                    minSdkVersion 23
                    targetSdkVersion 27

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_11
                    targetCompatibility JavaVersion.VERSION_11
                }
            }
        }
    }
}

apply from: 'dependencies.gradle'

task clean(type: Delete) {
    delete rootProject.buildDir
}
